# The 2-sphere

```agda
module synthetic-homotopy-theory.2-sphere where
```

<details><summary>Imports</summary>

```agda
open import foundation.action-on-identifications-dependent-functions
open import foundation.action-on-identifications-functions
open import foundation.constant-type-families
open import foundation.contractible-maps
open import foundation.contractible-types
open import foundation.dependent-pair-types
open import foundation.equivalences
open import foundation.fibers-of-maps
open import foundation.function-extensionality
open import foundation.function-types
open import foundation.functoriality-dependent-pair-types
open import foundation.homotopies
open import foundation.identity-types
open import foundation.propositions
open import foundation.sections
open import foundation.transport-along-identifications
open import foundation.universe-levels

open import structured-types.pointed-equivalences
open import structured-types.pointed-types

open import synthetic-homotopy-theory.circle
open import synthetic-homotopy-theory.double-loop-spaces
open import synthetic-homotopy-theory.free-2-loops
open import synthetic-homotopy-theory.spheres
```

</details>

## Idea

The [spheres](synthetic-homotopy-theory.spheres.md) are defined as
[iterated-suspensions](synthetic-homotopy-theory.iterated-suspensions.md)
of the [standard two-element type `Fin 2`](univalent-combinatorics.standard-finite-types.md).
The 2-sphere is the result of suspending twice. Thus, the 2-sphere has
all the universal properties expected of suspensions.

The 2-sphere can equivalently be defined as a type generated by a point
and a 2-loop, giving rise to the point and 2-loop version of [the universal
property of the 2-sphere](synthetic-homotopy-theory.2-sphere.md)

## Definitions

```agda
𝕊²-Pointed-Type : Pointed-Type lzero
𝕊²-Pointed-Type = sphere-Pointed-Type 2

𝕊² : UU lzero
𝕊² = sphere 2

base-𝕊² : 𝕊²
base-𝕊² = north-sphere 2

south-𝕊² : 𝕊²
south-𝕊² = south-sphere 2

meridian-𝕊² : sphere 1 → base-𝕊² ＝ south-𝕊²
meridian-𝕊² = meridian-sphere 1

meridian-base-𝕊² : base-𝕊² ＝ south-𝕊²
meridian-base-𝕊² = meridian-sphere 1 (north-sphere 1)

meridian-loop-𝕊² : meridian-base-𝕊² ＝ meridian-base-𝕊²
meridian-loop-𝕊² = ap (meridian-sphere 1) north-sphere-1-loop

surf-𝕊² : type-Ω² base-𝕊²
surf-𝕊² =
  map-pointed-equiv
    ( pointed-equiv-2-loop-pointed-identity
      ( 𝕊²-Pointed-Type)
      ( meridian-base-𝕊²))
    ( meridian-loop-𝕊²)

free-2-loop-𝕊² : free-2-loop 𝕊²
pr1 free-2-loop-𝕊² = base-𝕊²
pr2 free-2-loop-𝕊² = surf-𝕊²

```
